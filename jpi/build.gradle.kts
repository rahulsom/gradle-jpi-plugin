import okio.buffer
import okio.sink
import org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
import org.gradle.api.tasks.testing.logging.TestLogEvent
import java.nio.file.StandardOpenOption

buildscript {
    dependencies {
        classpath(libs.okhttp)
    }
}

plugins {
    groovy
    `kotlin-dsl`
    signing
    codenarc
    alias(libs.plugins.plugin.publish)
    `java-gradle-plugin`
}

description = "Gradle plugin for building and packaging Jenkins plugins"

dependencies {
    // Dependencies moved from core module
    compileOnly(libs.accessmodifier.checker)
    annotationProcessor(libs.sezpoz)
    implementation(libs.sezpoz)
    compileOnly(libs.javapoet) {
        because("used for GenerateTestTask")
    }
    compileOnly(libs.jenkins.test.harness) {
        because("used for GenerateTestTask")
        isTransitive = false
    }
    compileOnly(libs.junit4) {
        because("used for GenerateTest")
    }
    compileOnly(libs.maven.plugin.api)

    // JPI-specific dependencies
    compileOnly(libs.localizer.maven)
    implementation(libs.spotbugs.gradle)
    implementation(libs.commons.io)
    implementation(localGroovy())
    implementation(libs.jgit)

    // Test dependencies
    testImplementation(testFixtures(project(":core")))
    testImplementation(libs.spock.core)
    testImplementation(libs.xmlunit)
    testImplementation(libs.commons.text)
    testImplementation(libs.accessmodifier.checker)
    testImplementation(libs.javapoet)
    testImplementation(libs.jenkins.core) {
        exclude(module = "groovy-all")
    }
    testImplementation(platform(libs.junit5.bom))
    testImplementation(libs.junit5.api)
    testImplementation(libs.assertj.core)
    testImplementation(libs.awaitility)
    testImplementation(libs.maven.model)
    testRuntimeOnly(libs.junit5.jupiter)
    testRuntimeOnly(libs.junit5.vintage)
    testCompileOnly(libs.junit4) {
        because("used for generated tests with javapoet")
    }
    testCompileOnly(libs.develocity.testing.annotations)
}

publishing {
    publications {
        create<MavenPublication>("pluginMaven") {
            pom {
                name.set("Gradle JPI Plugin")
                description.set("The Gradle JPI plugin is a Gradle plugin for building Jenkins plugins")
                url.set("http://github.com/jenkinsci/gradle-jpi-plugin")
                scm {
                    url.set("https://github.com/jenkinsci/gradle-jpi-plugin")
                }
                licenses {
                    license {
                        name.set("Apache 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                        distribution.set("repo")
                    }
                }
                developers {
                    developer {
                        id.set("abayer")
                        name.set("Andrew Bayer")
                    }
                    developer {
                        id.set("kohsuke")
                        name.set("Kohsuke Kawaguchi")
                    }
                    developer {
                        id.set("daspilker")
                        name.set("Daniel Spilker")
                    }
                    developer {
                        id.set("sghill")
                        name.set("Steve Hill")
                    }
                }
            }
        }
    }
}

signing {
    val signingKeyId: String? by project
    val signingKey: String? by project
    val signingPassword: String? by project
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    setRequired { setOf("jenkins.username", "jenkins.password").all { project.hasProperty(it) } }
}

tasks.addRule("Pattern: testGradle<ID>") {
    val taskName = this
    if (!taskName.startsWith("testGradle")) return@addRule
    val task = tasks.register(taskName)
    for (javaVersion in listOf(17)) {
        val javaSpecificTask = tasks.register<Test>("${taskName}onJava${javaVersion}") {
            val gradleVersion = taskName.substringAfter("testGradle")
            systemProperty("gradle.under.test", gradleVersion)
            setTestNameIncludePatterns(listOf("*IntegrationSpec"))
            javaLauncher.set(javaToolchains.launcherFor {
                languageVersion.set(JavaLanguageVersion.of(javaVersion))
            })
        }
        task.configure {
            dependsOn(javaSpecificTask)
        }
    }
}

val isCi = providers.environmentVariable("CI")
tasks.withType<Test>().configureEach {
    useJUnitPlatform()
    testLogging {
        if (isCi.map { it.toBoolean() }.getOrElse(false)) {
            events(TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED)
        }
        exceptionFormat = FULL
    }
}

codenarc {
    toolVersion = "1.6"
    configFile = rootProject.file("config/codenarc/rules.groovy")
}

tasks.codenarcTest {
    configFile = rootProject.file("config/codenarc/rules-test.groovy")
}

gradlePlugin {
    plugins {
        create("pluginMaven") {
            id = "org.jenkins-ci.jpi"
            implementationClass = "org.jenkinsci.gradle.plugins.jpi.JpiPlugin"
            displayName = "A plugin for building Jenkins plugins"
            website.set("https://wiki.jenkins-ci.org/display/JENKINS/Gradle+JPI+Plugin")
            vcsUrl.set("https://github.com/jenkinsci/gradle-jpi-plugin")
            description = "A plugin for building Jenkins plugins"
            tags.set(listOf("jenkins"))
        }
    }
}

fun Project.stringProp(named: String): String? = findProperty(named) as String?

tasks.register("shadeLatestVersionNumber") {
    doLast {
        val response = okhttp3.OkHttpClient().newCall(okhttp3.Request.Builder().get()
                .url("https://raw.githubusercontent.com/jenkinsci/lib-version-number/master/src/main/java/hudson/util/VersionNumber.java")
                .build())
                .execute()
        if (!response.isSuccessful) {
            throw GradleException("${response.code} attempting to fetch latest hudson.util.VersionNumber")
        }
        val extension = project.extensions.getByType(JavaPluginExtension::class)
        val main = extension.sourceSets.getByName("main")
        val srcMainJava = main.java.srcDirs.single().toPath()
        val dest = srcMainJava.resolve("shaded/hudson/util/VersionNumber.java")
        val sink = dest.sink(StandardOpenOption.CREATE, StandardOpenOption.TRUNCATE_EXISTING).buffer()
        response.body?.use {
            it.source().use { input ->
                sink.use { output ->
                    output.writeAll(input)
                }
            }
        }
    }
}

val checkPhase = tasks.named("check")
val publishToGradle = tasks.named("publishPlugins")
publishToGradle.configure {
    dependsOn(checkPhase)
}

rootProject.tasks.named("postRelease").configure {
    dependsOn(publishToGradle)
}
